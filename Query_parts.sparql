PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sphaera: <http://ontologies.mpiwg-berlin.mpg.de/sphaera/>
PREFIX sphType: <http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/>

SELECT DISTINCT
?custom_identifier
?label
?subject
?title
?period
(GROUP_CONCAT(DISTINCT ?dataset_label; SEPARATOR=", ") as ?dataset_labels)
(GROUP_CONCAT(DISTINCT ?corpus_label; SEPARATOR=", ") as ?corpora_labels)
(GROUP_CONCAT(DISTINCT ?author_name; SEPARATOR=", ") as ?authors)
(GROUP_CONCAT(DISTINCT ?sort_author_name; SEPARATOR=", ") as ?sort_authors)

WHERE{

   #BIND(<http://sphaera.mpiwg-berlin.mpg.de/id/part/c6145ca3-317c-4724-a344-3c2e106fe26b> as ?subject)
?subject a sphaera:Part .
?subject ecrm:P3_has_note ?label .

OPTIONAL{
?subject ecrm:P4_has_time-span ?period_uri.
?period_uri ecrm:P82_at_some_point_within ?period.
}

OPTIONAL{
  ?subject ^sphaera:partlink ?book .
}

OPTIONAL{
  ?subject ^ecrm:P15_was_influenced_by/^efrbroo:R17i_was_created_by ?part.   
  ?part ^sphaera:partlink ?book .
}

OPTIONAL {
  ?book ecrm:P12_occurred_in_the_presence_of ?dataset .
  <http://sphaera.mpiwg-berlin.mpg.de/id/thes/dataset> <http://www.w3.org/2004/02/skos/core#member> ?dataset .
  ?dataset ecrm:P3_has_note ?dataset_label .
}

OPTIONAL {
  ?book ecrm:P12_occurred_in_the_presence_of ?corpus .
  <http://sphaera.mpiwg-berlin.mpg.de/id/thes/corpus> <http://www.w3.org/2004/02/skos/core#member> ?corpus .
  ?corpus ecrm:P3_has_note ?corpus_label .
}


OPTIONAL {
  ?subject ecrm:P1_is_identified_by ?identifier .
  ?identifier ecrm:P2_has_type sphaera:customIdentifier ;
  ecrm:P3_has_note ?custom_identifier_str .
  BIND(xsd:integer(?custom_identifier_str) as ?custom_identifier)
}


OPTIONAL {
  ?subject ecrm:PC01_is_domain_of ?has_title .
  ?has_title a ecrm:PC102_has_title ;
    ecrm:P102.1_has_type sphType:primary ;
    ecrm:PC02_has_range/ecrm:P3_has_note ?part_title .
}


OPTIONAL {
  ?subject efrbroo:R17i_was_created_by/ecrm:PC01_is_domain_of/ecrm:PC02_has_range/ecrm:P48_has_preferred_identifier/ecrm:P3_has_note ?author_label .
  BIND(LCASE(?author_label) AS ?sort_author_name)
}
BIND( IF(BOUND(?part_title), ?part_title, "&nbsp;") as ?title )
   
  

  } 


GROUP BY ?subject 
?title 
?custom_identifier 
?label 
?period